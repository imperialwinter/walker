#define TAK

#include "general/standard_commands.h"

piece base, pod, bar, barrelpt1, barrelpt2, flare1, flare2, wing1, wing2, e1;

static-var  gun_1;

#define SMOKEPIECE1 base
#define SMOKEPIECE2 base
#define SMOKEPIECE3 base
#define SMOKEPIECE4 base
#include "general/smokeunit_sws.h"

#define MED_MUZZLE_FLASH_FX_GREEN 1024+1

Create() {
	hide flare1;
	hide flare2;
	gun_1 = 0;
	start-script SmokeUnit_SWS();
}

QueryWeapon1(piecenum) {
	if(!gun_1) {
		piecenum = barrelpt2;
	}
	if(gun_1) {
		piecenum = barrelpt1;
	}
}

AimWeapon1(heading, pitch) {
	turn barrelpt1 to y-axis heading now;
	turn barrelpt2 to y-axis heading now;
	turn barrelpt1 to x-axis 0 - pitch now;
	turn barrelpt2 to x-axis 0 - pitch now;
	return 1;
}

FireWeapon1() {
	if(!gun_1) {
		emit-sfx MED_MUZZLE_FLASH_FX_GREEN from flare2;
	}
	if(gun_1) {
		emit-sfx MED_MUZZLE_FLASH_FX_GREEN from flare1;
	}
	gun_1 = !gun_1;
}

Killed(severity, corpsetype) {
	if(severity <= 25) {
		explode wing1 type FALL | SMOKE;
		explode pod type FALL | SMOKE | FIRE;
		return 0;
	}
	if(severity <= 50) {
		explode wing2 type FALL | SMOKE;
		explode pod type FALL | SMOKE | FIRE;
		return 0;
	}
	explode wing1 type FALL | SMOKE;
	explode wing2 type FALL | SMOKE | FIRE;
	explode pod type SHATTER | SMOKE | FIRE;
}
